MODULE BootstrapSystem;
  IMPORT SYSTEM, Kernel, Display, Fonts, Texts, Oberon, PCLinkCompile;

  VAR W: Texts.Writer;
    T: Texts.Text;
    tX, tY, tC: INTEGER;

  PROCEDURE NotifyDisplay (T: Texts.Text; op: INTEGER; beg, end: LONGINT);
    VAR R: Texts.Reader; nextCh: CHAR;
      patadr, dx, x, y, w, h: INTEGER;
  BEGIN
    IF op = Texts.insert THEN
      Texts.OpenReader(R, T, beg); Texts.Read(R, nextCh);
      WHILE ~R.eot DO
        IF nextCh = 0DX THEN
          tX := 0; DEC(tY, Fonts.Default.height);
          IF tY < Fonts.Default.height THEN
            tC := (tC + Display.Width DIV 2) MOD Display.Width;
            tY := Display.Height - Fonts.Default.height;
            Display.ReplConst(Display.black, tC - 10, 0, Display.Width DIV 2, Display.Height, Display.replace)
          END
        ELSE
          Fonts.GetPat(Fonts.Default, nextCh, dx, x, y, w, h, patadr);
          IF (tX + x + w < Display.Width DIV 2 - 20) & (h # 0) THEN
            Display.CopyPattern(Display.white, patadr, tX + tC + x, tY + y, Display.invert)
          END; INC(tX, dx)
        END; Texts.Read(R, nextCh)
      END
    END
  END NotifyDisplay;

  PROCEDURE Trap(VAR a: INTEGER; b: INTEGER);
    VAR u, v, w: INTEGER;
  BEGIN u := SYSTEM.REG(15); SYSTEM.GET(u - 4, v); w := v DIV 10H MOD 10H; (*trap number*)
    IF w = 0 THEN Kernel.New(a, b)
    ELSE Texts.WriteString(W, "TRAP"); Texts.WriteInt(W, w, 4);
      Texts.WriteLn(W); Texts.Append(Oberon.Log, W.buf); Oberon.Reset
    END
  END Trap;

  PROCEDURE Abort;
    VAR n: INTEGER;
  BEGIN n := SYSTEM.REG(15); Texts.WriteString(W, "ABORT "); Texts.WriteHex(W, n);
    Texts.WriteLn(W); Texts.Append(Oberon.Log, W.buf); Oberon.Reset
  END Abort;

BEGIN Texts.OpenWriter(W); tX := 0; tY := Display.Height - Fonts.Default.height; tC := 10;
  Kernel.Install(SYSTEM.ADR(Trap), 20H); Kernel.Install(SYSTEM.ADR(Abort), 0);
  Display.ReplConst(Display.black, 0, 0, Display.Width, Display.Height, Display.replace);
  NEW(T); T.notify := NotifyDisplay; Texts.Open(T, ""); Oberon.OpenLog(T);
  Texts.WriteString(W, "Oberon BootstrapSystem"); Texts.WriteLn(W); Texts.Append(Oberon.Log, W.buf);
  PCLinkCompile.Run()
END BootstrapSystem.
